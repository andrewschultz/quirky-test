
start_at = nameless_lameness
start_theme = my_theme

## we start with simplest defined then move to more complex

## Booleans and integers

booleans {
   ## game state booleans
   in_debug: boolean "false" ;
   score_notify: boolean "true" ;
   tutorial_done: boolean "false" ;
   footnotes_available : boolean "false" scope="survivor";

   ## regular point tally booleans
   point_first_base : boolean "false" ;
   thor_talk: boolean "false" ;

   ## bonus point tally booleans
   bonus_point_turkey_quest : boolean "false" ;
   bonus_point_theory_bore : boolean "false" ;

   ## tutorial booleans
   in_tutorial: boolean "false" ;
   tutorial_about_done : boolean "false" ;
   tutorial_verbs_done : boolean "false" ;
   tutorial_x_me_done : boolean "false" ;
   tutorial_x_me_cued : boolean "false" ;
   tutorial_item_taken: boolean "false" ;
   tutorial_additional_metaverb_done : boolean "false" ;
   tutorial_x_ask_done : boolean "false" ;

   ## parser shortcut booleans
   look_warn: boolean "false" ;
   examine_warn: boolean "false" ;
   inventory_warn : boolean "false" ;
   long_word_warn : boolean "false" ;
}

integers {
   tutorial_score : integer "0" ;
   max_tutorial_score : integer "1";
   score : integer "0" ;
   max_score : integer "1" ;
   bonus_score : integer "0" ;
   max_bonus_score : integer "2" ;
}

## strings

strings {
   ## short stubs
   # meta verbs in orange
   abt : string "<ABOUT<#f80>>" ;
   exits : string "<EXITS<#f80>>" ;
   exitserr : dynamic_string {( is_beside "cheering_voice" ? "No, that's not the way out. Trial and error might get it. Or figuring what the voice reall means." :
      "There's no exit that way. {exits} will show which there are." )} ;
   hint : string "<HINT<#f80>> or <HINTS<#f80>>" ;
   meta : string "<META<#f80>>" ;
   sco : string "<SCORE<#f80>>" ;
   thx : string "<THANKS<#f80>>" ;
   verbs : string "<VERBS<#f80>>" ;

   # yes-or-no
   yn : string "<YES<#f88>>/<Y<#f88>> or <NO<#f88>>/<N<#f88>>" ;
   on : string "<ON<#f88>>" ;
   off : string "<OFF<#f88>>" ;

   # options in blue 00f
   notif : string "<NOTIFY<#00f>>" ;

   # game title
   qt : string "<Quirky Test<#ff0>>" ;

   # regular commands in green
   lishea : string "<LISTEN<#0f0>> or <HEAR<#0f0>>" ;
   xme : string "<X ME<#0f0>>" ;

   # tutorial stuff in purple
   tut : string "<TUTORIAL NOTE:<#f0f>> " ;
   otn : string "<ONE-TIME NOTE:<#f0f>> " ;

   ## dynamic strings

   my_score : dynamic_string {( score + "/" + max_score )}
   notify_string : string {( "You need to specify " + notif + " " + yn + " for score notification changes." )}

   ## location strings
   burst_face_after : string "You're at first base now." ;
   burst_face_before : string "You're at a burst face. You feel about ready to take an initial step." ;
}

## Objects with vocabulary

objects {
  ooh_task : object "Ooh Task to Ask ('Ooh Task')" msg="It is a request to find the <Rare-Thing-There Ring<#a33>>.";
  cheering_voice : scenery "cheering voice" msg="It gives a cheery \"Whoa! Guest!\"" conspicuous="false";
  beery_thor : object "Beery Thor" msg="Big and strong.";
}

vocabulary {
   : noun      / aliases = [ooh, task, ask]
}

## Locations with connections

locations {
   nameless_lameness      : location "You are surrounded by nameless lameness. It's all very, very grey." graphic = "lameness_16" {
      on_describe {
      : if ( is_beside "cheering_voice") {
         : print "The cheering voice still floats around. You can HEAR it or LISTEN.";
      }
      }
   };
   burst_face : location "{ point_first_base ? burst_face_after : burst_face_before }" {
      on_command {
         : match "talk _;talk thor"  {
            : print "\"Here's where I boom HOLD, BUB. What am I hiding from you? Just one more test before you get to the main area.\"";
            : set_true "thor_talk" ;
         }
         : match "bold hub" {
            : if (thor_talk) {
               : print "\"Yes, that's where we'll go next.\"";
               : gosub "add_point" ;
               : press_any_key ;
               : create "beery_thor" target = "bold_hub" ;
               : goto "bold_hub";
               : redescribe ;
            }
         }
      };
   };
   bold_hub  : location "You are in a bold hub. You can go in pretty much any direction." ;
}

connections {

   ## Connects locations together (bidirectional)

   from, direction, to = [
      ## bold_hub,     north, bold_hub
   ]
}

barriers {
   block_disorientate : block {
      block_when_not  = tutorial_x_ask_done
      message         = In this nameless lameness, any move without figuring where to go could get you even more lost. You need some clue, first.
   }
}
subroutines {
   add_point : subroutine {
      : if (score_notify) {
         : print "<Your score just went up by a point!<#ff0>>";
      }
      : increment "score";
      }
   add_bonus : subroutine {
      : if (score_notify) {
         : print "<You just found a bonus point!<6>>" ;
         : if (bonus_score == 0) {
             : print {( otn + "There are a few of these in " + qt + ", and " + sco + " will let you know your progress there." )};
         }
      }
      : increment "bonus_score";
      }
   init : subroutine {
   : print "QUIRKY TEST: a tutorial of sorts by Andrew Schultz!" ;
   : print "Submitted for the 2025 TALP competition." ;
   : press_any_key ;
   : if (footnotes_available) {
     : print "Because you went through once, footnotes are now available.";
     : press_any_key ;
     }
   : clear_screen ;
   }
   tutorial_choice : subroutine {
   : ask_bool { ##? how do we put QT in here?
      question   = <Quirky Test<#ff0>> has a tutorial mode for the first few puzzles. Would you like to use it?
      yes_answer = Yes, I'd like to use the tutorial.
      no_answer  = No, I don't need the tutorial.
      var        = in_tutorial
      }
      : print {( "Note you can change in and out of tutorial mode with <TUTORIAL<#00f>> " + yn + " before you have completed the first few tasks, though this may break narrative continuity." )} ;
      : press_any_key ;
      : clear_screen ;
   }
   long_words_note : subroutine {
      : if (!long_word_warn) {
      : print "<ALSO<#f0f>>: in general, longer words will work, but parser conventions have evolved to help save the player keystrokes." ;
      : set_true "long_word_warn" ;
      }
   }
   debug_commands : subroutine {
      : match "wingame _"  {
         : print {( footnotes_available ? "Already won game, but restarting with footnotes still available." : "Auto-winning game so footnotes are available on reboot." )} ;
         : set_true "footnotes_available";
         : win_game ;
      }
   }
   options_list : subroutine {
      : print {( "The only current option is " + notif + ", for score notification." )};
      : print {( "Note that we use " + yn + " to toggle options instead of " + on + " and " + off + ", since " + on + " is reserved by Adventuron." )};
      : print {( "Also note that a verb alone will not be recognized. At least, not in the initial version of " + qt + "." )} ;
   }
   options_notify: subroutine {
      : match "notify yes;notify y" {
         : print "Score notify is now on.";
         : set_true "score_notify";
         : done ;
      }
      : match "notify no;notify n" {
         : print "Score notify is now off.";
         : set_false "score_notify";
         : done ;
      }
      : match "notify -" {
         : print {( "Toggling <NOTIFY<#0f0>> without an argument is not supported. " + notify_string )};
         : done ;
      }
      : match "notify *" {
         : print {( "The argument after <NOTIFY<#0f0>> wasn't recognized. " + notify_string )};
      }
   }
   tutorial_progress : subroutine {
      : if (!tutorial_about_done) {
         : print {( tut + "An author frequently implements an " + abt + " commands page for the player to access. This contains details that might disrupt the cover-screen narrative. You can try it now." )} ;
         : return ;
      }
      : if (!tutorial_verbs_done) {
         : print {( tut + "The " + verbs + " meta-command is useful to see what the most common commands are. You can try it now." )};
         : return ;
      }
      : if (!tutorial_additional_metaverb_done) {
         : print {( tut + "The " + abt + " and " + verbs + " meta-commands tipped off additional other meta-commands (listed in orange) that may help you get your bearings. You should try one now." )};
         : return ;
      }
      : if (!tutorial_x_me_done) {
         : print {( tut + (tutorial_x_me_cued ? xme + " is a standard command to start text adventures, to get your bearings. You'll want to type it now to continue the tutorial." : "Now you've looked into the meta-commands, you'll want to have a look at yourself. One convention of text adventures is that " + xme + " provides clues of who you are and what you're doing with relatively few keystrokes. So " + xme + " should be your first in-world command.")  )} ;
         : set_true "tutorial_x_me_cued" ;
         : return ;
      }
      : if (!tutorial_x_ask_done) {
         : print {( tut + "Now that you have found something, the last thing to do is to examine it with <X TASK<#0f0>>. Adventuron also recognizes <X ASK<#0f0>> and <X OOH<#0f0>>, but not <X TO<#0f0>>, as that's a reserved word." )} ;
         : return;
      }
      : if (!is_beside "cheering_voice") {
         : print {( tut + "sometimes a game will indicate you should use one of your senses." )};
         : return;
      }
      : print {( tut + "the \"Whoa! Guest!\" must be a hint. Perhaps you should determine which direction it wants you to go." )} ;
      : return ;
   }

   meta_about : subroutine {
      : print {( qt + " was made for the TALP 2025 jam after years of judging it. I always wanted to write an Adventuron game." )} ;
      : print {( "You can find specific thanks in the " + thx + " command." )} ;
      : set_true "tutorial_about_done" ;
   }
   meta_thanks : subroutine {
      : print "Thanks to TALP admins past and future.";
      : print "Thanks to Chris Ainsley (sp?) for creating Adventuron.";
      : print "Thanks to my testers.";
      : print "Thanks to you, for playing this game.";
      : gosub "check_additional_meta" ;
   }

   check_additional_meta : subroutine {
      : if (!tutorial_additional_metaverb_done) {
         : print {( tut + "good job. You found one specific meta-command. There are others, but I didn't want to force you to see them all. You can if you want. You just need to know that a robust text adventure takes steps to help the player not feel lost." )} ;
      }
      : set_true "tutorial_additional_metaverb_done";
   }

   verb_list : subroutine {
      : set_true "tutorial_verbs_done";
      : print {( "While " + qt + " requires you to guess some verbs as part of a puzzle, it doesn't require any nonstandard verbs to move around or manipulate objects or NPCs." )} ;
      : print "There are no diagonal directions, only N S E W U D and IN and OUT. You can use the long form of each direction, but it is not necessary." ;
      : print "You can <TAKE<#0f0>> an item, but there is no need to <DROP<#0f0>> it. You can also <X<#0f0>> it, which is short for <EXAMINE<#0f0>>." ;
      : print {( "No other standard commands are needed to win " + qt + ". However, " + meta + " will describe meta-commands, and <OPTIONS<#f80>> toggles options." )} ;
      : if (in_debug) {
         : print "In debug mode, <WINGAME<#0f0>> will win the game to open up footnotes." ;
      }
   }
   verb_meta_list : subroutine {
      : print {( abt + " gives info about the game." )} ;
      : print {( thx + " gives info about people who helped with the game. I usually use <CREDITS<#f80>>, but Adventuron reserves that for its own." )} ;
      : print {( "Adventuron also offers the standard parser meta-commands of <SAVE<#f80>>, <RESTORE<#f80>> and <QUIT<#f80>>, to save a game for later, come back to it, or quit." )} ;
      : print {( "Adventuron-specific " + meta + " commands include <TSTART<#f80>> and <TSTOP<#f80>>, which start and stop a transcript." )} ;
      : set_true "tutorial_additional_metaverb_done";
   }
   verb_options_list : subroutine {
      : print {( "Many games have options for sound, etc. " + qt + " uses <OPTIONS YES/Y<#00f>> and <OPTIONS NO/N<#00f>> to change them. <OPTION<#00f>> shows its status." )} ;
      : print {( "The main option is <TUTORIAL<#0f0>>" + (tutorial_done ? "but you've finished the tutorial" : ", though it's disabled once the tutorial is done" ) + "." )} ;
   }
}

themes {
   my_theme : theme {
      status_bar {
         : header_text;
         : dynamic_text "my_score" ;
      }
      theme_settings {
         parser_delay  = 0
      }
      colors {
         status_bar_pen   = 15
         status_bar_paper = 9
      }
      system_messages {

   ask_new_game                                   = Would you like to restart {qt}?
   ask_quit                                       = Do you wish to quit {qt}?
   post_quit = I hope you enjoyed the experience. Pressing any key will restart {qt}.
   you_cant_go_that_direction                     = {exitserr}
   unknown_verb                                   = Nothing comes up from that. {verbs} gives a list of basic standard verbs, though you will have a few to guess based on rules.
   }
      }
   }

## On_debug and on_startup call the INIT subroutine, but on_debug sets a flag or flags to allow for cheat commands

on_debug {
   : set_true "in_debug" ;
   : gosub "init" ;
   : gosub "tutorial_choice" ;
}

on_startup {
   : gosub "init" ;
   : gosub "tutorial_choice" ;
}

## on_tick is primarily used for the tutorial

on_tick {
   : if (is_at "nameless_lameness" && is_carried "ooh_task" && !tutorial_x_ask_done && noun1_is "task" ) {
      : print "But the ring's probably not here. Where can you go? You feel you are stumbling around. You hear the silence shattered. Perhaps you can use one of your senses and pick up a clue." ;
      : set_true "tutorial_x_ask_done" ;
   }
   : else_if (is_at "nameless_lameness" && is_carried "ooh_task" && tutorial_x_ask_done && !is_beside "cheering_voice" ) {
      : print "But it's no longer silent. Perhaps you can <LISTEN<#0f0>> or <HEAR<#0f0>>.";
   }
   : if (in_tutorial) {
      : gosub "tutorial_progress" ;
   }
}

## on_describe is for moving around

on_describe {
}

## Test VERB NOUN that the player types and executes conditional actions

on_pre_command {
   : match "west _" {
      : if (is_beside "cheering_voice") {
      : gosub "add_point" ;
      : print "The voice says \"Yes, yes. That is the way.\" The fogginess clears a bit, and as go west, a large, strong man waves to you. \"I'm Beery Thor. Nice to meet you. Got a few things to share with you. Just TALK to me...";
      : if (in_tutorial) {
         : print {( "Congratulations! You've finished the tutorial. Now on with the game. While the tutorial is over, you can still use " + hint + ", though <HELP<#f80>>, another often useful command, is reserved by Adventuron for general parser guidance." )};
      }
      : set_true "tutorial_done";
      : set_false "in_tutorial";
      : press_any_key;
      : goto "burst_face" ;
      : create "beery_thor" ;
      : redescribe ;
      }
   }
   : if (!examine_warn) {
      : match "examine *;look *" {
         : if (original "verb" == "examine") {
            : print {( otn + " to save keystrokes, you only need to type <X<#0f0>> (noun) to examine." )} ;
            : gosub "long_words_note" ;
            : set_true "examine_warn" ;
            : press_any_key ;
         }
      }
   }
   : if (!look_warn) {
      : match "look -" {
         : if (original "verb" == "look") {
            : print {( otn + " you only need to type <L<#0f0>> to look." )} ;
            : gosub "long_words_note" ;
            : set_true "look_warn" ;
            : press_any_key ;
            : redescribe ;
         }
      }
   }
   : if (!inventory_warn) {
      : match "inventory _" {
         : if (original "verb" == "inventory") {
            : print {( otn + " you only need to type <I<#0f0>> to take inventory." )} ;
            : gosub "long_words_note" ;
            : set_true "inventory_warn" ;
            : press_any_key ;
         }
      }
   }
}

on_command {
   : match "salesmen _"  {
      : if (is_at "nameless_lameness") {
         : print "Good guess, if this were an anagram game." ;
         }
      }
   : match "theory bore" {
      : if (is_beside "beery_thor" && bonus_point_theory_bore) {
         : print {( "Thor is " + (point_first_base ? "" : "likely ") + "bigger than you. Best not to overplay this." )} ;
      }
      : else {
        : print {( point_first_base ? "You snicker at what you've found, but then figure you better not do so to Thor's face." : "You worry Thor will be upset by this observation, but he booms \"Ha! Good one. I was sort of hoping you'd figure that. I actually don't mind people saying that. It implies I said something too smart. Better than being called a musclehead or whatever.\"" )} ;
       : set_true "bonus_point_theory_bore" ;
       : gosub "add_bonus" ;
    }
   }
   : match "turkey quest" {
      : if (bonus_point_turkey_quest) {
         : print "Hey, now. I'll poke fun at myself a bit, but repeating the joke might make things a bit stale." ;
         }
      : else {
         : print "Ah! Yes, you are in the spirit of things, here. Have a bonus point." ;
         : set_true "bonus_point_turkey_quest" ;
         : gosub "add_bonus";
      }
   }
   : if (in_debug) {
      : gosub "debug_commands" ;

   }
   : match "exits _" {
      : if (is_at "nameless_lameness") {
         : print "There's a puzzle here to start. The tutorial can walk you through it." ;
         }
      : else {
         : print "I should list exits here.";
      }
   }
   : match "verb _;verbs _" {
      : gosub "verb_list" ;
   }
   : match "about _" {
      : gosub "meta_about" ;
   }
   : match "thank _;thanks _" {
      : gosub "meta_thanks" ;
   }
   : match "drop _" {
      : print "While classic text adventures often required the player to use <DROP<#0f0>> due to inventory limits, this is often regarded as needless fiddling today. Text adventures often take steps to keep the player's inventory manageable so they can focus on puzzles, getting rid of items that are no longer necessary.";
   }
   : match "listen _; hear _" {
      : if (is_at "nameless_lameness" && tutorial_x_ask_done) {
         : print "The Veering Choice Cheering Voice greets you, sort of, with a \"Whoa! Guest!\"" ;
         : create "cheering_voice" ;
      }
      : else {
            : print "You don't hear anything right now." ;
      }
   }
   : match "go west" {
      : print "1";
      : if (is_beside "cheering_voice") {
         : print "Yes, the voice says, that is the way!" ;
         : goto "burst_face" ;
      }
   }
   : match "x me" {
      : if (is_carried "ooh_task") {
         : print "Got what you need." ;
      }
      : else {
         : pocket "ooh_task" ;
         : print "You search yourself, wondering if you forgot something, and hoping something will turn up. And guess what? There's something called <Ooh, Task to Ask<#00f>>." ;
         : print "You're not sure who slipped it in there, or how, but all the same, you're glad you might have some direction, now." ;
         : gosub "add_point";
         : set_true "tutorial_x_me_done";
      }
   }
   : match "x ooh;x task;x ask" {
      : if (is_carried "ooh_task" && !tutorial_x_ask_done) {
         : mask {
         }
      }
   }
   : match "first base" {
      : if (is_at "burst_face") {
         : if (point_first_base) {
            : print "Already changed things." ;
			: return ;
         }
		 : set_true "point_first_base" ;
		 : create "beery_thor" ;
		 : gosub "add_point" ;
	  }
   }
   : match "score _"  {
      : if (in_tutorial) {
         : print {( "Your <tutorial score<3>> is " + tutorial_score + " out of <" + max_tutorial_score + "<#ff0>>." )} ;
      }
      : print {( "Your <core score<4>> is " + score + " out of <" + max_score + "<#f00>>." )} ;
      : print {( "Your <bonus score<#f80>> is " + bonus_score + " out of <" + max_bonus_score + "<#f00>>." )} ;
      }
   : match "option _;options _;opts _;opt _" {
      : gosub "options_list";
   }
   : match "notify _" {
      : gosub "options_notify";
   }
   : match "meta _" {
      : gosub "verb_meta_list";
   }
}


assets {
   graphics {
      lameness_16 : base64_png "iVBORw0KGgoAAAANSUhEUgAAAQAAAAAoBAMAAADj1Dm6AAAAMFBMVEVSUlKtra2fn5+YmJimpqZgYGBnZ2dubm5ZWVl1dXWRkZGDg4NLS0u0tLR8fHyKioo9s6mBAAAB6ElEQVRYw+XYIZaDMBAGYHIELrACvQ6JRdUjse0RKmvpARBcAIHEcgFEZS0KzxXovja0ZCYzJARq9r8A3yOTySSeJyN8meGVPnym+SR45SaTPXMtZBKZtJsSP1O9c5xylrmMf/HemQA/CiCCgN8J0G4NEAuAWgsoICCZvn9/AQ5rAQMBuOkBuTvA0wJCqgQAIEGAOAZrYAoYDAFKCeQIcLcHCBUAtkHNAhIEiJ0BoAgCUIO3+QrMfkAKAYfVgH4OqCEg+zIgAICWWoEUlMAHUMrvnwyLUC0CY0C3OSDSArIvAAYdQPkBmi7gABAEINQBsozog+l6gG8BaNcAKh4AjwIAqHcH+DYAOAyQZyELOP87QMkBBAmIdIB2c4C/AGi4Rjirwq4jzwJ+CXy8CwflPNadRRsCxFaAlAaU3wXcScBRB/BJQMQAPjNxYQBg54FlQL09YHQAtBCQE/eS2VD4Bpw4wGwX9jsDLmaAhgNcdwWENoACAdDFoASAcdRtQ1dAQgOqVYCo4XaBpggYQMkAyMOwsQXgIqiITrQE6HWX42VA4ggAUzn5PIEAhSNAIAD/PkIDUC+kbqcXK0C9NwBN5b0tIKeGIrQCchsAgNC9TyBAYAHoqE6o7EP+jSqC7yMmALgG6AcovfgBhBvnaozG45IAAAAASUVORK5CYII=";
   }
}
