
start_at = nameless_lameness
start_theme = my_theme

## we start with simplest defined then move to more complex

## Booleans and integers

booleans {
   ## game state booleans
   in_debug : boolean "false" ;
   score_notify : boolean "true" ;
   tutorial_done : boolean "false" ;
   footnotes_available : boolean "false" scope="survivor";
   talked_to_thor : boolean "false";

   ## regular point tally booleans
   point_first_base : boolean "false" ;

   ## QT goals, north then clockwise
   point_master_plan : boolean "false" ;
   point_chalice_map : boolean "false" ;
   point_fell_short : boolean "false" ;
   point_take_crackers : boolean "false" ;
   point_shift_goals : boolean "false" ;
   point_hunters_grill : boolean "false" ;
   point_fellow_yield : boolean "false" ;

   ## TQ goals, north then clockwise
   point_flaring_score : boolean "false" ;
   point_bending_truth : boolean "false" ;
   point_boring_way: boolean "false" ;
   point_dirt_home : boolean "false" ;
   point_roaring_pain : boolean "false" ;
   point_money_fountain : boolean "false" ;
   point_grim_tune : boolean "false" ;

   ## quasi-point
   fh_travel_grail : boolean "false" ;

   ## bonus point tally booleans
   bonus_point_turkey_quest : boolean "false" ;
   bonus_point_theory_bore : boolean "false" ;
   bonus_point_death_sentence : boolean "false" ;

   ## tutorial booleans, organized by order you see/do them in
   in_tutorial: boolean "false" ;
   tutorial_about_done : boolean "false" ;
   tutorial_verbs_done : boolean "false" ;
   tutorial_additional_metaverb_done : boolean "false" ;
   tutorial_x_me_done : boolean "false" ;
   tutorial_x_me_cued : boolean "false" ;
   tutorial_inventory_done : boolean "false" ;
   tutorial_inventory_cued : boolean "false" ;
   tutorial_item_taken: boolean "false" ;
   tutorial_x_ask_done : boolean "false" ;

   ## parser shortcut booleans
   look_warn: boolean "false" ;
   examine_warn: boolean "false" ;
   inventory_warn : boolean "false" ;
   long_word_warn : boolean "false" ;
}

integers {
   tutorial_score : integer "0" ;
   max_tutorial_score : integer "1";
   score : integer "0" ;
   max_score : integer "5" ;
   bonus_score : integer "0" ;
   max_bonus_score : integer "3" ;
}

## strings

strings {
   ## short stubs
   # meta verbs in orange
   abt : string "<ABOUT<#f80>>" ;
   exits : string "<EXITS<#f80>>" ;
   exitserr : dynamic_string {( is_beside "cheering_voice" ? "No, that's not the way out. Trial and error might get it. Or figuring what the voice really means." :
      tutorial_x_ask_done ? "In this nameless lameness, any move without figuring where to go could get you even more lost. You need some clue, first." :
      is_at "nameless_lameness" ? "You don't want to wandering off without any direction, but maybe that voice could offer a clue." :
      "There's no exit that way. {exits} will show which there are." )} ;
   hint : string "<HINT<#f80>> or <HINTS<#f80>>" ;
   meta : string "<META<#f80>>" ;
   sco : string "<SCORE<#f80>>" ;
   thx : string "<THANKS<#f80>>" ;
   verbs : string "<VERBS<#f80>>" ;

   # yes-or-no
   yn : string "<YES<#f88>>/<Y<#f88>> or <NO<#f88>>/<N<#f88>>" ;
   on : string "<ON<#f88>>" ;
   off : string "<OFF<#f88>>" ;

   # options in blue 00f
   notif : string "<NOTIFY<#00f>>" ;

   # game title
   qt : string "<Quirky Test<#ff0>>" ;

   # regular commands in green
   lishea : string "<LISTEN<#0f0>> or <HEAR<#0f0>>" ;
   xme : string "<X ME<#0f0>>" ;

   # tutorial stuff in purple
   tnote : string "<TUTORIAL NOTE:<#f0f>> " ;
   tcue : string "<TUTORIAL CUE:<#f0f>> " ;
   otn : string "<ONE-TIME NOTE:<#f0f>> " ;

   ## dynamic strings

   my_score : dynamic_string {( score + "/" + max_score )}
   notify_string : string {( "You need to specify " + notif + " " + yn + " for score notification changes." )}

   ## location strings
   burst_face_after : string "You're at first base now." ;
   burst_face_before : string "You're at a burst face. You feel about ready to take an initial step." ;

   sub_hub: string "Another mini-hub here. You can go up over a hill, south back to the bold hub, or in any of the eight directions." ;

   ## quickie trips seeding for dynamic strings
   qt_plaster_man : string "A plaster man stands here. Perhaps there is something inside it." ;
   qt_master_plan : string "With the plaster man destroyed, there's not much left here." ;
   qt_gift_shoals : string "Gift shoals are here. Yet no matter how many gifts they wash up, you don't see anything you like." ;
   qt_shift_goals : string "The shoals seem more peaceful now that you have fixed your mindset." ;

   ## tricky quips seeding for dynamic strings
   tq_hurt_dome : string "hurt dome" ;
   tq_dirt_home : string "dirt home" ;

   ## fate hills
   fate_hills_desc: dynamic_string {( fh_travel_grail ? "Well, finding the travel grail wasn't your ultimate fate, but it probably helped you get here. Not much more to do." : "You feel something fateful in the air. Like you need to do something, and you need to be prepared."
   )};
}

## Objects with vocabulary

objects {
  # in inventory
  ooh_task : object "Ooh Task to Ask ('Ooh Task')" msg="It is a request to find the <Rare-Thing-There Ring<#a33>>.";

  # scenery
  cheering_voice : scenery "cheering voice" msg="It gives a cheery \"Whoa! Guest!\"" conspicuous="false";
  seth_dentons : scenery "Seth Denton's" msg="It looks forbidding, but you're not sure why." at="bold_hub" conspicuous="false";

  # NPC(s)
  beery_thor : object "Beery Thor" msg="Big and strong and friendly.";
}

vocabulary {
   : noun / aliases = [ooh, task, ask]
   : verb / aliases = [tut, tutorial]
}

## Locations and location-specific parser rules

locations {
   nameless_lameness      : location "You are surrounded by nameless lameness. It's all very, very grey." graphic = "lameness_16" {
      on_command {
         : if ((verb_is "inventory") && in_tutorial && !tutorial_x_me_done) {
            : print "Taking inventory is a good thing to do early in a text adventure, but we're not there yet in the tutorial. Once you've found an item, the tutorial will nudge you to do take inventory.";
         }
         : match "listen _; hear _" {
            : if (tutorial_x_ask_done) {
               : print "You hear a voice. It's a happy one. For whatever reason, you mark it up as a Veering Choice Cheering Voice. It greets you, sort of, with a \"Whoa! Guest!\" You'd like to see where it's from. But which direction is that?" ;
               : create "cheering_voice" ;
               : return ;
            }
         }
         : match "x me" {
            : if (in_tutorial && (!tutorial_about_done || !tutorial_verbs_done || !tutorial_additional_metaverb_done)) {
               : print {( "While " + xme + " is a useful command, and one you'll be using in this tutorial, we're a bit on rails. There are meta-commands to acquaint yourself with first." )};
            }
            : else_if (is_carried "ooh_task") {
               : print "Got what you need." ;
            }
            : else {
               : pocket "ooh_task" ;
               : print "You search yourself, wondering if you forgot something, and hoping something will turn up. And guess what? There's something called <Ooh, Task to Ask<#00f>>." ;
               : print "You're not sure who slipped it in there, or how, but all the same, you're glad you might have some direction, now." ;
               : gosub "add_point";
               : set_true "tutorial_x_me_done";
            }
         }
         : match "go west" {
            : if (is_beside "cheering_voice") {
               : print "Yes, the voice says, that is the way!" ;
               : goto "burst_face" ;
            }
         }
      }
      on_describe {
         : if ( is_beside "cheering_voice") {
            : print "The cheering voice still floats around. You can HEAR it or LISTEN.";
         }
         : else_if (is_carried "ooh_task" && tutorial_x_ask_done) {
            : print "But it's no longer silent. Perhaps you can <LISTEN<#0f0>> or <HEAR<#0f0>>.";
         }
      }
   };
   burst_face : location "{ point_first_base ? burst_face_after : burst_face_before }" {
      on_command {
         : match "talk _;talk thor"  {
            : if (!talked_to_thor) {
               : print "\"Well, there's one last thing to do, here. You managed to GO WEST after hearing me say Whoa, Guest. What can you do with the burst face?\"";
               : set_true "talked_to_thor";
               : if (in_tutorial) {
                  : print "Congratulations! You've finished the tutorial." ;
               }
            }
            : else {
               : print {( point_first_base ? "\"Here's where I boom HOLD, BUB. What am I hiding from you? Just one more test before you get to the main area.\"" : "\"You need to figure what to do with the burst face that's blocking your way.\"" )};
               : set_true "talked_to_thor" ;
            }
         }
         : match "first base" {
            : if (point_first_base) {
               : print "You already changed things here." ;
               : return ;
            } : else_if (!talked_to_thor) {
               : print "\"That's the right idea, but I'm bigger than you. I'm going to make you talk to me first. Don't worry, I'm interesting. Or I think I am.\"";
               : return ;
            }
            : print "Ah, there we go. That'll make paths clearer.";
            : set_true "point_first_base" ;
            : create "beery_thor" ;
            : gosub "add_point" ;
         }
         : match "bold hub" {
            : if (talked_to_thor) {
               : print "\"Yes, that's where we'll go next.\"";
               : gosub "add_point" ;
               : press_any_key ;
               : create "beery_thor" target = "bold_hub" ;
               : goto "bold_hub";
               : redescribe ;
            }
         }
      }
   };
   bold_hub  : location "You are in a bold hub. You can go in pretty much any direction." {
      on_command {
         : if (is_beside "seth_dentons") {
            : match "death sentence" {
               : print "\"Yes, that's it. This is one of those traps where you're given adequate warning that going somewhere might kill you.\"";
               : set_true "bonus_point_death_sentence" ;
               : destroy "seth_dentons" ;
               : gosub "add_bonus" ;
            }
         }
      }
      on_describe {
         : if (is_beside "seth_dentons") {
            : print "An establishment called Seth Denton's is off to the side here. You have a feeling you don't want to go IN, and maybe you can figure why." ;
         }
      }
   };

   quickie_trips: location "QUICKIE TRIPS\n{sub_hub}";
   tricky_quips: location "TRICKY QUIPS\n{sub_hub}";
   fate_hills: location "FATE HILLS\n{fate_hills_desc}\nYou can go back west to the quickie trips or east to the tricky quips.";

   qt_n : location "{point_master_plan ? qt_master_plan : qt_plaster_man}" {
      on_command {
      : match "master plan" {
         : if (point_master_plan) {
            : print "You already got the master plan.";
            }
         : else
         {
            : print "You have reason to find a master plan, so you bust open the master plan and find it." ;
            : gosub "add_point" ;
            : set_true "point_master_plan" ;
            : update_graphic ;
         }
         : done ;
         }
      }
   };
   qt_ne : location "Unwritten.";
   qt_e : location "Unwritten.";
   qt_se : location "Unwritten.";
   qt_sw : location "{point_shift_goals ? qt_shift_goals : qt_gift_shoals}" {
      on_command {
      : match "shift goals" {
         : if (point_shift_goals) {
            : print "But you don't want to shift your goals back." ;
            }
         : else
         {
            : print "You stop looking for something direct from the gift shoals. You begin, like, meditating and stuff." ;
            : gosub "add_point" ;
            : set_true "point_shift_goals" ;
            : update_graphic ;
         }
         : done ;
         }
      }
   };
   qt_w : location "Unwritten.";
   qt_nw : location "Unwritten.";

   tq_n : location "Unwritten.";
   tq_ne : location "Unwritten.";
   tq_e : location "Unwritten.";
   tq_se : location "There's a {point_dirt_home ? tq_dirt_home : tq_hurt_dome} here." {
      on_command {
      : match "dirt home" {
         : if (point_dirt_home) {
            : print "You already made the dirt home." ;
            }
         : else
         {
            : print "Poof! The hurt dome transforms into a dirt home." ;
            : gosub "add_point" ;
            : set_true "point_dirt_home" ;
            : update_graphic ;
         }
         : done ;
         }
      }
   };
   tq_sw : location "Unwritten.";
   tq_w : location "Unwritten.";
   tq_nw : location "Unwritten.";

};

# connections between locations. There are warps for the first few areas, but after that, it's a pretty standard map.

connections {

   from, direction, to = [

      ## two-way stuff first
      tricky_quips, north, tq_n,
      tricky_quips, northeast, tq_ne,
      tricky_quips, east, tq_e,
      tricky_quips, southeast, tq_se,
      tricky_quips, southwest, tq_sw,
      tricky_quips, west, tq_w,
      tricky_quips, northwest, tq_nw,

      quickie_trips, north, qt_n,
      quickie_trips, northeast, qt_ne,
      quickie_trips, east, qt_e,
      quickie_trips, southeast, qt_se,
      quickie_trips, southwest, qt_sw,
      quickie_trips, west, qt_w,
      quickie_trips, northwest, qt_nw,

      ## one-way stuff below

      bold_hub, west_oneway, quickie_trips,
      quickie_trips, south_oneway, bold_hub,
      bold_hub, east_oneway, tricky_quips,
      tricky_quips, south_oneway, bold_hub,
      tricky_quips, up_oneway, fate_hills,
      quickie_trips, up_oneway, fate_hills,
      fate_hills, west_oneway, quickie_trips,
      fate_hills, east_oneway, tricky_quips
   ]
}

barriers {
   block_qt_n : block {
      location = qt_n
      block_when = point_master_plan
      message = You got the master plan, so you don't really need to go back.
   }
   block_qt_sw : block {
      location = qt_sw
      block_when = point_shift_goals
      message = With your goals shifted, you've gotten all the mindfulness or whatever you need from the shoals.
   }
   block_tq_se : block {
      location = tq_se
      block_when = point_dirt_home
      message = You changed the hurt dome to a dirt home. You don't need to do much else.
   }
}

subroutines {
   add_point : subroutine {
      : if (score_notify) {
         : print "<Your score just went up by a point!<#ff0>>";
      }
      : increment "score";
      }
   add_bonus : subroutine {
      : if (score_notify) {
         : print "<You just found a bonus point!<6>>" ;
         : if (bonus_score == 0) {
             : print {( otn + "There are a few of these in " + qt + ", and " + sco + " will let you know your progress there." )};
         }
      }
      : increment "bonus_score";
      }
   init : subroutine {
   : print "QUIRKY TEST: a tutorial of sorts by Andrew Schultz!" ;
   : print "Submitted for the 2025 TALP competition." ;
   : press_any_key ;
   : if (footnotes_available) {
     : print "Because you went through once, footnotes are now available.";
     : press_any_key ;
     }
   : clear_screen ;
   }
   tutorial_choice : subroutine {
   : ask_bool { ##? how do we put QT in here?
      question   = <Quirky Test<#ff0>> has a tutorial mode for the first few puzzles. Would you like to use it?
      yes_answer = Yes, I'd like to use the tutorial.
      no_answer  = No, I don't need the tutorial.
      var        = in_tutorial
      }
      : print {( "Note you can change in and out of tutorial mode with <TUTORIAL<#00f>> " + yn + " before you have completed the first few tasks, though this may break narrative continuity." )} ;
      : press_any_key ;
      : clear_screen ;
   }
   long_words_note : subroutine {
      : if (!long_word_warn) {
      : print "<ALSO<#f0f>>: in general, longer words will work, but parser conventions have evolved to help save the player keystrokes." ;
      : set_true "long_word_warn" ;
      }
   }
   debug_commands : subroutine {
      : match "wingame _"  {
         : print {( footnotes_available ? "Already won game, but restarting with footnotes still available." : "Auto-winning game so footnotes are available on reboot." )} ;
         : set_true "footnotes_available";
         : win_game ;
      }
   }
   options_list : subroutine {
      : print {( "The only current option is " + notif + ", for score notification." )};
      : print {( "Note that we use " + yn + " to toggle options instead of " + on + " and " + off + ", since " + on + " is reserved by Adventuron." )};
      : print {( "Also note that a verb alone will not be recognized. At least, not in the initial version of " + qt + "." )} ;
   }
   options_notify: subroutine {
      : match "notify yes;notify y" {
         : print "Score notify is now on.";
         : set_true "score_notify";
         : done ;
      }
      : match "notify no;notify n" {
         : print "Score notify is now off.";
         : set_false "score_notify";
         : done ;
      }
      : match "notify -" {
         : print {( "Toggling <NOTIFY<#0f0>> without an argument is not supported. " + notify_string )};
         : done ;
      }
      : match "notify *" {
         : print {( "The argument after <NOTIFY<#0f0>> wasn't recognized. " + notify_string )};
      }
   }
   options_tutorial: subroutine {
      : match "tutorial yes;tutorial y" {
         : print "Score notify is now on.";
         : set_true "score_notify";
         : done ;
      }
      : match "tutorial no;tutorial n" {
         : print "Tutorial mode is now off.";
         : set_false "score_notify";
         : done ;
      }
      : match "tutorial -" {
         : print {( "Toggling <TUTORIAL<#0f0>> without an argument is not supported. " + notify_string )};
         : done ;
      }
      : match "tutorial *" {
         : print {( "The argument after <TUTORIAL<#0f0>> wasn't recognized. " + notify_string )};
      }
   }
   tutorial_progress : subroutine {
      : if (!tutorial_about_done) {
         : print {( tcue + "An author frequently implements an " + abt + " commands page for the player to access. This contains details that might disrupt the cover-screen narrative. You can try it now." )} ;
         : return ;
      }
      : if (!tutorial_verbs_done) {
         : print {( tcue + "The " + verbs + " meta-command is useful to see what the most common commands are. You can try it now." )};
         : return ;
      }
      : if (!tutorial_additional_metaverb_done) {
         : print {( tcue + "The " + abt + " and " + verbs + " meta-commands tipped off additional other meta-commands, listed in <orange<#f80>>, that may help you get your bearings. You should try one now." )};
         : return ;
      }
      : if (!tutorial_x_me_done) {
         : print {( tcue + (tutorial_x_me_cued ? xme + " is a standard command to start text adventures, to get your bearings. You'll want to type it now to continue the tutorial." : "Now you've looked into the meta-commands, you'll want to have a look at yourself. One convention of text adventures is that " + xme + " provides clues of who you are and what you're doing with relatively few keystrokes. So " + xme + " makes sense as your first in-world command.")  )} ;
         : set_true "tutorial_x_me_cued" ;
         : return ;
      }
      : if (!tutorial_inventory_done) {
         : print {( tcue + "Now you examined yourself with " + xme + ", another common command is to take inventory to verify you are carrying the item you found. This can be shortened with <I<#f80>>." )} ;
         : return;
      }
      : if (!tutorial_x_ask_done) {
         : print {( tcue + "Now that you verified you found something new, the last thing to do is to examine it with <X TASK<#0f0>>. Adventuron also recognizes <X ASK<#0f0>> and <X OOH<#0f0>>, but not <X TO<#0f0>>, as that's a reserved word." )} ;
         : return;
      }
      : if (!is_beside "cheering_voice") {
         : print {( tcue + "sometimes a game will indicate you should use one of your senses. While guess-the-verb is frowned upon in parser games, the lack of senses hopefully makes the guessing straightforward." )};
         : return;
      }
      : print {( tcue + "the \"Whoa! Guest!\" must be a hint. Perhaps you should determine which direction it wants you to go." )} ;
      : return ;
   }

   meta_about : subroutine {
      : print {( qt + " was made for the TALP 2025 jam after years of judging it. I always wanted to write an Adventuron game." )} ;
      : print {( "You can find specific thanks in the " + thx + " command." )} ;
      : set_true "tutorial_about_done" ;
   }
   meta_thanks : subroutine {
      : print "Thanks to TALP admins past and future.";
      : print "Thanks to Chris Ainsley (sp?) for creating Adventuron.";
      : print "Thanks to my testers.";
      : print "Thanks to you, for playing this game.";
      : gosub "check_additional_meta" ;
   }

   check_additional_meta : subroutine {
      : if (!tutorial_additional_metaverb_done) {
         : print {( tnote + "You found one specific meta-command. There are others, but I didn't want to force you to see them all. You can if you want. You just need to know that a robust text adventure takes steps to help the player not feel lost. This wasn't always the case back in the '80s, when disk space was much scarcer." )} ;
      }
      : set_true "tutorial_additional_metaverb_done";
   }

   verb_list : subroutine {
      : set_true "tutorial_verbs_done";
      : print {( "While " + qt + " requires you to guess some verbs as part of a puzzle, it doesn't require any nonstandard verbs to move around or manipulate objects or NPCs." )} ;
      : print "There are no diagonal directions, only N S E W U D and IN and OUT. You can use the long form of each direction, but it is not necessary." ;
      : print "You can <TAKE<#0f0>> an item, but there is no need to <DROP<#0f0>> it. You can also <X<#0f0>> it, which is short for <EXAMINE<#0f0>>." ;
      : print {( "No other standard commands are needed to win " + qt + ". However, " + meta + " will describe meta-commands, and <OPTIONS<#f80>> toggles options." )} ;
      : if (in_debug) {
         : print "In debug mode, <WINGAME<#0f0>> will win the game to open up footnotes." ;
      }
   }
   verb_meta_list : subroutine {
      : print {( abt + " gives info about the game." )} ;
      : print {( thx + " gives info about people who helped with the game. I usually use <CREDITS<#f80>>, but Adventuron reserves that for its own." )} ;
      : print {( "Adventuron also offers the standard parser meta-commands of <SAVE<#f80>>, <RESTORE<#f80>> and <QUIT<#f80>>, to save a game for later, come back to it, or quit." )} ;
      : print {( "Adventuron-specific " + meta + " commands include <TSTART<#f80>> and <TSTOP<#f80>>, which start and stop a transcript." )} ;
      : set_true "tutorial_additional_metaverb_done";
   }
   verb_options_list : subroutine {
      : print {( "Many games have options for sound, etc. " + qt + " uses <OPTIONS YES/Y<#00f>> and <OPTIONS NO/N<#00f>> to change them. <OPTION<#00f>> shows its status." )} ;
      : print {( "The main option is <TUTORIAL<#0f0>>" + (tutorial_done ? "but you've finished the tutorial" : ", though it's disabled once the tutorial is done" ) + "." )} ;
   }
}

themes {
   my_theme : theme {
      status_bar {
         : header_text;
         : dynamic_text "my_score" ;
      }
      theme_settings {
         parser_delay  = 0
      }
      colors {
         status_bar_pen   = 15
         status_bar_paper = 9
      }
      system_messages {

   ask_new_game                                   = Would you like to restart {qt}?
   ask_quit                                       = Do you wish to quit {qt}?
   invalid_choice                                 = Please choose YES/Y or NO/N.
   post_quit = I hope you enjoyed the experience. Pressing any key will restart {qt}.
   you_cant_go_that_direction                     = {exitserr}
   unknown_verb                                   = Nothing comes up from that. {verbs} gives a list of basic standard verbs, though you will have a few to guess based on rules.
   }
      }
   }

## On_debug and on_startup call the INIT subroutine, but on_debug sets a flag or flags to allow for cheat commands

on_debug {
   : set_true "in_debug" ;
   : gosub "init" ;
   : gosub "tutorial_choice" ;
}

on_startup {
   : gosub "init" ;
   : gosub "tutorial_choice" ;
}

## on_tick is primarily used for the tutorial

on_tick {
   : if (is_at "nameless_lameness" && verb_is "i" && is_carried "ooh_task") {
      : set_true "tutorial_inventory_done" ;
   }
   : if (is_at "nameless_lameness" && is_carried "ooh_task" && !tutorial_x_ask_done && noun1_is "task" ) {
      : print "But the ring's probably not here. Where can you go? You feel you are stumbling around. You hear the silence shattered. Perhaps you can use one of your senses and pick up a clue." ;
      : set_true "tutorial_x_ask_done" ;
   }
   : if (in_tutorial) {
      : gosub "tutorial_progress" ;
   }
}

## on_describe is for moving around

on_describe {
}

## Test VERB NOUN that the player types and executes conditional actions

on_pre_command {
   : match "west _" {
      : if (is_beside "cheering_voice") {
      : gosub "add_point" ;
      : print "The voice says \"Yes, yes. That is the way.\" The fogginess clears a bit, and as go west, a large, strong man waves to you. \"I'm Beery Thor. Nice to meet you. Got a few things to share with you. Just TALK to me...";
      : if (in_tutorial) {
         : print {( "Congratulations! You've finished the tutorial. Now on with the game. While the tutorial is over, you can still use " + hint + ", though <HELP<#f80>>, another often useful command, is reserved by Adventuron for general parser guidance." )};
      }
      : set_true "tutorial_done";
      : set_false "in_tutorial";
      : press_any_key;
      : goto "burst_face" ;
      : create "beery_thor" ;
      : redescribe ;
      }
   }
   : if (!examine_warn) {
      : match "examine *;look *" {
         : if (original "verb" == "examine") {
            : print {( otn + " to save keystrokes, you only need to type <X<#0f0>> (noun) to examine." )} ;
            : gosub "long_words_note" ;
            : set_true "examine_warn" ;
            : press_any_key ;
         }
      }
   }
   : if (!look_warn) {
      : match "look -" {
         : if (original "verb" == "look") {
            : print {( otn + " you only need to type <L<#0f0>> to look." )} ;
            : gosub "long_words_note" ;
            : set_true "look_warn" ;
            : press_any_key ;
            : redescribe ;
         }
      }
   }
   : if (!inventory_warn) {
      : match "inventory _" {
         : if (original "verb" == "inventory") {
            : print {( otn + " you only need to type <I<#0f0>> to take inventory." )} ;
            : gosub "long_words_note" ;
            : set_true "inventory_warn" ;
            : press_any_key ;
         }
      }
   }
}

on_command {
   : match "salesmen _"  {
      : if (is_at "nameless_lameness") {
         : print "Good guess, if this were an anagram game." ;
         }
      }
   : match "theory bore" {
      : if (is_beside "beery_thor" && bonus_point_theory_bore) {
         : print {( "Thor is " + (point_first_base ? "" : "likely ") + "bigger than you. Best not to overplay this." )} ;
      }
      : else {
        : print {( point_first_base ? "You snicker at what you've found, but then figure you better not do so to Thor's face." : "You worry Thor will be upset by this observation, but he booms \"Ha! Good one. I was sort of hoping you'd figure that. I actually don't mind people saying that. It implies I said something too smart. Better than being called a musclehead or whatever.\"" )} ;
       : set_true "bonus_point_theory_bore" ;
       : gosub "add_bonus" ;
    }
   }
   : match "turkey quest" {
      : if (bonus_point_turkey_quest) {
         : print "Hey, now. I'll poke fun at myself a bit, but repeating the joke might make things a bit stale." ;
         }
      : else {
         : print "Ah! Yes, you are in the spirit of things, here. Have a bonus point." ;
         : set_true "bonus_point_turkey_quest" ;
         : gosub "add_bonus";
      }
   }
   : if (in_debug) {
      : gosub "debug_commands" ;

   }
   : match "exits _" {
      : if (is_at "nameless_lameness") {
         : print "There's a puzzle here to start. The tutorial can walk you through it." ;
         }
      : else {
         : print "I should list exits here.";
      }
   }
   : match "verb _;verbs _" {
      : gosub "verb_list" ;
   }
   : match "about _" {
      : gosub "meta_about" ;
   }
   : match "thank _;thanks _" {
      : gosub "meta_thanks" ;
   }
   : match "drop _" {
      : print "While classic text adventures often required the player to use <DROP<#0f0>> due to inventory limits, this is often regarded as needless fiddling today. Text adventures often take steps to keep the player's inventory manageable so they can focus on puzzles, getting rid of items that are no longer necessary.";
   }
   : match "listen _; hear _" {
      : print "You don't hear anything right now." ;
   }
   : match "x ooh;x task;x ask" {
      : if (is_carried "ooh_task" && !tutorial_x_ask_done) {
         : mask {
         }
      }
   }
   : match "score _"  {
      : if (in_tutorial) {
         : print {( "Your <tutorial score<3>> is " + tutorial_score + " out of <" + max_tutorial_score + "<#ff0>>." )} ;
      }
      : print {( "Your <core score<4>> is " + score + " out of <" + max_score + "<#f00>>." )} ;
      : print {( "Your <bonus score<#f80>> is " + bonus_score + " out of <" + max_bonus_score + "<#f00>>." )} ;
      }
   : match "option _;options _;opts _;opt _" {
      : gosub "options_list";
   }
   : match "notify _" {
      : gosub "options_notify";
   }
   : match "tutorial _" {
      : gosub "options_tutorial";
   }
   : match "meta _" {
      : gosub "verb_meta_list";
   }
}


assets {
   graphics {
      lameness_16 : base64_png "iVBORw0KGgoAAAANSUhEUgAAAQAAAAAoBAMAAADj1Dm6AAAAMFBMVEVSUlKtra2fn5+YmJimpqZgYGBnZ2dubm5ZWVl1dXWRkZGDg4NLS0u0tLR8fHyKioo9s6mBAAAB6ElEQVRYw+XYIZaDMBAGYHIELrACvQ6JRdUjse0RKmvpARBcAIHEcgFEZS0KzxXovja0ZCYzJARq9r8A3yOTySSeJyN8meGVPnym+SR45SaTPXMtZBKZtJsSP1O9c5xylrmMf/HemQA/CiCCgN8J0G4NEAuAWgsoICCZvn9/AQ5rAQMBuOkBuTvA0wJCqgQAIEGAOAZrYAoYDAFKCeQIcLcHCBUAtkHNAhIEiJ0BoAgCUIO3+QrMfkAKAYfVgH4OqCEg+zIgAICWWoEUlMAHUMrvnwyLUC0CY0C3OSDSArIvAAYdQPkBmi7gABAEINQBsozog+l6gG8BaNcAKh4AjwIAqHcH+DYAOAyQZyELOP87QMkBBAmIdIB2c4C/AGi4Rjirwq4jzwJ+CXy8CwflPNadRRsCxFaAlAaU3wXcScBRB/BJQMQAPjNxYQBg54FlQL09YHQAtBCQE/eS2VD4Bpw4wGwX9jsDLmaAhgNcdwWENoACAdDFoASAcdRtQ1dAQgOqVYCo4XaBpggYQMkAyMOwsQXgIqiITrQE6HWX42VA4ggAUzn5PIEAhSNAIAD/PkIDUC+kbqcXK0C9NwBN5b0tIKeGIrQCchsAgNC9TyBAYAHoqE6o7EP+jSqC7yMmALgG6AcovfgBhBvnaozG45IAAAAASUVORK5CYII=";
      masterplan_after : base64_png "iVBORw0KGgoAAAANSUhEUgAAAQAAAAAoBAMAAADj1Dm6AAAAMFBMVEUAAACAAAAAgACAgAAAAICAAIAAgICAgIDAwMD/AAAA/wD//wAAAP//AP8A//////97H7HEAAAAiUlEQVRYw+3VuRGAMAxEUfdfihI1QkVKFl+4ATPzk9VArIcu2gNHM8AAAwwwwAADDDDAAANwQMBxA9B8K4UAVOPpyZWFAE5acS2IXfwkAD13ja9XLwUyA70DWg2AAPEBKoqbgdsBvN6CNYTQHThrWNAdiNV8UXcgdw1UyoDWMPdfgGnBzEoCfosXzzjyF/ZYz+8AAAAASUVORK5CYII=";
      plasterman_before : base64_png "iVBORw0KGgoAAAANSUhEUgAAAQAAAAAoBAMAAADj1Dm6AAAAMFBMVEUAAACAAAAAgACAgAAAAICAAIAAgICAgIDAwMD/AAAA/wD//wAAAP//AP8A//////97H7HEAAAAhElEQVRYw+3UywkAIQyE4RRg/0XkYgtbgLCteA7CrGwJIvz7yNwlH0OiNTiWgAQkIAHLL88BAyQWcErj3w3MHShwA8PoHThQgKSOAwoK6FYK3IDRgNES8AmAL2YCwjfkvYC6SWCr46PKa4CAKXBHAeIBAQOcBsQDAGIB9yliH9GuJCABF2/W9E17T7/QAAAAAElFTkSuQmCC";
      dirthome_after : base64_png "iVBORw0KGgoAAAANSUhEUgAAAQAAAAAoBAMAAADj1Dm6AAAAMFBMVEUAAACAAAAAgACAgAAAAICAAIAAgICAgIDAwMD/AAAA/wD//wAAAP//AP8A//////97H7HEAAAAy0lEQVRYw+3VMQ6DMAyF4XcFlJNY7M19uRZ7lxwgLUKiHYhkWy/JgD0gFqKPP0Hg7RhZbid51oLjmX1pzGsQQFqANAbQDOBKAGIAVwIwA3gSgBnAkwDUAI4EoAZwJAA3gD0BuAHsCUAOYE4AcgBzArADWBOgWkY0gGRa0gQoq2pyN0DWAUovQKk6QM2dAFkLKL22QAuojwGcX93v9jtjAWmV4/J3K88AXK0DMOsQxhYE4Dh5MvkQnoBZ/4LrVQMQAApgs4wOYFpyOuAD2wKbyFO+eg8AAAAASUVORK5CYII=";
      hurtdome_before : base64_png "iVBORw0KGgoAAAANSUhEUgAAAQAAAAAoBAMAAADj1Dm6AAAAMFBMVEUAAACAAAAAgACAgAAAAICAAIAAgICAgIDAwMD/AAAA/wD//wAAAP//AP8A//////97H7HEAAABi0lEQVRYw8WXXY7FIAiFzw5Yf1c1W+G1Twwo/lRvJrk0Mr1pa65RP+EAFvLmgl4k76aID+W6uDL8DwBjb2UCMPChmQiAvnF9M6UDsN3Up+BsgLo2lmYiAJrzWwyEnYA3EWCvqZkIAO5CcPdz1AQxANoBJBOAHWASIb9T0/c5oBrCNEAOEPQBYjFYVlNDQMUHnwdpADARaCFi014RgcYiBU0QAahrwYTwoz91P1WqJAD2Uei1gNwvSQC6Ho0o7C+S0NkEAQn2rdM8hRoCOQDSt4r7uq6mPcR8gNCItulb5OZhjkgcIBADw/szQOs7DVB3SaMCeh2MngoQkEDzgyUC8jTQKkIWgGXgsnah0AckC2DOPvYoMbkE5lEAmgdSEyUtvSctQH8A4DwAu7tHMEznYwTTypcanI6iNEdkuTMAbPeuuv51XOQoWQAl+KjV4RoGUknOA0D6Aez5beiZiE4D9GW2r2PE4jAGsIzkYf0cAKzGxrNKHQfY5MaPrhwAbDU6GYC2v/MA+DMAQgC/9ff8qf8Ctc8AAAAASUVORK5CYII=";
      qt_n : dynamic_graphic {( point_master_plan ? "masterplan_after" : "plasterman_before" )} ;
      tq_se : dynamic_graphic {( point_dirt_home ? "dirthome_after" : "hurtdome_before" )} ;
   }
}
